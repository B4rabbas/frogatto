# Level triggers are carriers for custom code - code which will be triggered when a lever or pressure-plate is switched.  Normally the majority of their target objects are fixed-function devices like fans/platforms/gates; the purpose of this object is to be a catch-all for any weird custom things (like doing an off-level cutscene).

{
id: "cross_level_cutscene_initiator",
next_animation: "'normal'",
always_active: true,
is_strict: true,
hidden_in_game: true,
zorder: "@include data/zorder.cfg:in_front_of_everything",
prototype: ["triggerable"],
editor_info: {
	category: "controllers",
},


properties: {
	should_be_observed: "false",
	
		/*
			The event that keeps this cutscene from occurring twice.  If this is null, then this is a cutscene trigger that's supposed to be able to happen repeatedly, rather than just once.
		*/
	_gating_event: { type: "string|null", default: null },
	_cutscene_level: { type: "string|null", default: null },
	_cutscene_trigger_label: { type: "string", default: null, dynamic_initialization: true },
},

on_button_pressed: "if( (_gating_event = null) or
						(not level.player.get_stored_event(_gating_event) = 1),

						schedule(20,[
							spawn_player('invisible_observer', 0, 0, 1, 
								[
									set(child._player, level.player),
									set(child._post_cutscene_object, me.label),
									set(child._post_cutscene_level, level.id),
									set(child._cutscene_trigger_label, _cutscene_trigger_label),
								]
							),
							teleport(_cutscene_level,_cutscene_trigger_label,'instant'),
							screen_flash([0,0,0,255], [0,0,0,-5], 50)
						])
					)",

animation: {
	id: "normal",
	image: "effects/particles.png",
	x: 86, y: 73, w: 28, h: 28,
	duration: -1,
},
}
