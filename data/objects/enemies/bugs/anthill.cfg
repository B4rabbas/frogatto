{
id: "anthill",
is_strict: true,
activation_border: 1000,
prototype: ["standard_values"],

zorder: -1,
properties: {
	release_countdown_length: "int :: 200", //how many cycles before the visible ant poking up "pops out" of the hill

#-------------------------- editor vars --------------------------#
	_max_objects: { type: "int", editor_info: { type: "int", value: "3" } },
	_zone_x1: { type: "int", editor_info: { type: "x", value: "mid_x-300" } },
	_zone_x2: { type: "int", editor_info: { type: "x", value: "mid_x+300" } },


#-------------------------- temporary vars --------------------------#
	_spawning: { type: "obj movable_enemy|null", default: null, persistent: false },
	_release_counter: { type: "int", default: 0, persistent: false },

#-------------------------- storage --------------------------#
	_mook_tracker:	{ type: "class mook_spawning_tracker", init: "construct('mook_spawning_tracker',{this_obj: me, spawnee_types: ['ant_red','ant_red.thrown'], max_objects: 3})" },

#-------------------------- behavior --------------------------#
	hidden_zorder: "@include data/zorder.cfg:exterior_near_background",

	create_and_hold_child: "commands :: if(_spawning = null, [
			sound_falloff('ant-spawn.wav'),
			set(the_child.animation, 'stand'),
			set(_spawning, the_child),
			add_object(the_child),
		] where the_child = object('ant_red',mid_x,mid_y-6,{
			parent: me,
			collides_with_level: false,
			solid_dimensions_in: [],
			zorder:	hidden_zorder,
		})
	)",
	
	release_child: "commands :: if(_spawning != null, [
		set(_spawning.animation, 'walk'),
		set(_spawning.parent, null),
		set(_spawning.collides_with_level, true),
		set(_spawning, null),
		set(_spawning.velocity_x, 0),
		set(_spawning.velocity_y, -800),
		set(_spawning.zorder,0),
		set(_spawning.solid_dimensions_in, _spawning.default_solid_dimensions),
		set(_release_counter, 0)
	])",
},

on_process: "[
		if(_mook_tracker.should_create_a_new_mook or _spawning != null, add(_release_counter, 1), set(_release_counter, 0)),
		if(_release_counter >= (0.75 * release_countdown_length), create_and_hold_child ),
		if(_release_counter >= (1.00 * release_countdown_length), release_child)
	]",

on_create: "[set(_mook_tracker._x_bound, _zone_x1), set(_mook_tracker._x2_bound, _zone_x2)]",

on_end_normal_anim: "animation('normal')",

editor_info: {
	category: "enemies, general",
},
animation: {
	image: "enemies/anthill.png",
	id: "normal",
	rect: [1,1,39,33],
	frames: 1,
	duration: 10,
},
}
